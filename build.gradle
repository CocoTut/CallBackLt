// Top-level build file where you can add configuration options common to all sub-projects/modules.
import dependencies.BuildPlugins

buildscript {
    ext.kotlin_version = '1.3.61'

    repositories {
        google()
        jcenter()
        maven {url 'https://developer.huawei.com/repo/'}
    }
    dependencies {
        classpath BuildPlugins.GRADLE_ANDROID
        classpath BuildPlugins.KOTLIN
        classpath BuildPlugins.GRADLE_VERSIONS
        classpath BuildPlugins.GOOGLE_SERVICES
        classpath BuildPlugins.FIREBASE_CRASHLITYCS
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {url 'https://developer.huawei.com/repo/'}
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "com.github.ben-manes.versions"

            android {
                compileSdkVersion AndroidBuildConfig.COMPILE_SDK
                buildToolsVersion AndroidBuildConfig.BUILD_TOOLS_VERSION
                defaultConfig {
                    minSdkVersion AndroidBuildConfig.MIN_SDK
                    targetSdkVersion AndroidBuildConfig.TARGET_SDK
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                buildTypes {
                    debug {
                        manifestPlaceholders = [crashlyticsCollectionEnabled:"false"]
                    }

                    release {
                        manifestPlaceholders = [crashlyticsCollectionEnabled:"true"]
                        minifyEnabled true
                        proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                    }
                }

                compileOptions {
                    sourceCompatibility AndroidBuildConfig.javaVersion
                    targetCompatibility AndroidBuildConfig.javaVersion
                }

                kotlinOptions {
                    jvmTarget = '1.8'
                }

                packagingOptions {
                    exclude 'META-INF/AL2.0'
                    exclude 'META-INF/LGPL2.1'
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
